// SPDX-License-Identifier: BSD-3-Clause-Clear
// solhint-disable one-contract-per-file

pragma solidity >=0.8.19 <0.9.0;

import { console } from 'forge-std/console.sol';
import { Test } from 'forge-std/Test.sol';
import { MessageHashUtils } from '@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol';
import { EncryptedInput } from '@fhenixprotocol/cofhe-contracts/ICofhe.sol';
import { ZK_VERIFIER_SIGNER_PRIVATE_KEY } from '@cofhe/mock-contracts/contracts/MockCoFHE.sol';

/**
 * @dev Generates valid signatures for encrypted inputs.
 * Uses vm.sign to generate the signatures (only available in foundry tests)
 * Should not need to be interacted with directly, is part of the `createEncryptedInput` function set
 */
contract MockZkVerifierSigner is Test {
  function zkVerifySignPacked(
    EncryptedInput[] memory inputs,
    address sender,
    address consumingContract
  ) public view returns (EncryptedInput[] memory) {
    EncryptedInput[] memory signedInputs = new EncryptedInput[](inputs.length);
    for (uint256 i = 0; i < inputs.length; i++) {
      signedInputs[i] = zkVerifySign(inputs[i], sender, consumingContract);
    }
    return signedInputs;
  }

  /// @dev `consumingContract` binds the signature to the specific contract that will consume
  ///      this input (matching cofhe-contracts#77 / MockTaskManager.extractSigner), so a signed
  ///      input cannot be replayed into a different contract than the one it was signed for.
  function zkVerifySign(
    EncryptedInput memory input,
    address sender,
    address consumingContract
  ) public view returns (EncryptedInput memory) {
    bytes memory combined = abi.encodePacked(
      input.ctHash,
      input.utype,
      input.securityZone,
      sender,
      block.chainid,
      consumingContract
    );

    bytes32 expectedHash = keccak256(combined);

    (uint8 v, bytes32 r, bytes32 s) = vm.sign(ZK_VERIFIER_SIGNER_PRIVATE_KEY, expectedHash);
    bytes memory signature = abi.encodePacked(r, s, v); // note the order here is different from line above.

    input.signature = signature;
    return input;
  }
}
